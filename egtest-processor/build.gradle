apply plugin: 'maven'
apply plugin: 'kotlin'

dependencies {
    compile 'com.squareup:javapoet:1.8.0'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile project(':egtest-annotations')
    compile project(':kotlin-generator')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

// Everything below is for packaging releases

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// See http://central.sonatype.org/pages/manual-staging-bundle-creation-and-deployment.html
uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: "file://localhost/tmp/EgTest-staging/")

            pom.project {
                name 'EgTest Annotation Processor'
                packaging 'jar'
                description 'Java annotation processor for turning EgTest annotations into JUnit tests.'
                url 'https://github.com/dleppik/EgTest'

                scm {
                    connection 'scm:https://github.com/dleppik/EgTest.git'
                    developerConnection 'scm:git@/github.com:dleppik/EgTest.git'
                    url 'https://github.com/dleppik/EgTest/'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'dleppik'
                        name 'David Leppik'
                        email 'dleppik@vocalabs.com'
                    }
                }
            }
        }
    }
}
buildscript {
    ext.kotlin_version = '1.2.30'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
